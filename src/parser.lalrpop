use ast;
use lexer;
use std::str::FromStr;
use num::Num;

grammar<'input>;

pub Module: ast::Module = {
    <ModuleBlock> => ast::Module::Root {
        items: <>,
    }
};

ModuleBlock = <Item*>;

Item: ast::Item = {
    <MOD_DOC_COMMENT> => ast::Item::ModuleDocComment(<>.into()),
    <DOC_COMMENT> => ast::Item::DocComment(<>.into()),

    "use" <Expression> ";" => ast::Item::Use(<>),
    "extern" "mod" <Identifier> ";" => ast::Item::Extern(<>),
    "mod" <Identifier> => ast::Item::Module(ast::Module::Extern { name: <> }),
    "mod" <name:Identifier> "{" <items:ModuleBlock> "}" => ast::Item::Module(ast::Module::Inline {
        name: name,
        items: items,
    }),
    <Attributes> "type" <Identifier> ";" => ast::Item::TypeDecl(<>),
    <Attributes> "impl" <(<Identifier> "for")?> <Identifier> "{" <("fn" <Function>)*> "}" => {
        let (attrs, inter, name, methods) = (<>);
        ast::Item::TypeImpl(attrs, ast::TypeImpl {
            name: name,
            interface: inter,
            methods: methods,
        })
    },
    <Attributes> "trait" <Identifier> "{" <("fn" <TraitFunction>)*> "}" => {
        let (attrs, name, methods) = (<>);
        ast::Item::Trait(attrs, ast::Trait {
            name: name,
            methods: methods,
        })
    },
    <Attributes> "fn" <Function> => ast::Item::Function(<>),
};

Attributes: Vec<ast::Attribute> = ("@" <Attribute>)*;

Attribute: ast::Attribute = {
    <Identifier> => ast::Attribute::Identifier(<>),
    <Identifier> "(" <Comma<Attribute>> ")" => ast::Attribute::Named(<>),
    <StringLiteral> => ast::Attribute::String(<>),
};

Params = Comma<Identifier>;
Function: ast::Function = {
    <name:Identifier> "(" <params:Params> ")" <err_flag:("?")?> <body:Block> => {
        let mut func = ast::Function {
            name: name.into(),
            parameters: params,
            can_error: err_flag.is_some(),
            is_member: false,
            body: body,
        };
        
        if let Some("self") = func.parameters.first().map(|s| &s[..]) {
            func.parameters.remove(0);
            func.is_member = true;
        }

        func
    },
};

TraitFunction: ast::TraitFunction = {
    <name:Identifier> "(" <params:Params> ")" <err_flag:("?")?> ";" => {
        let mut func = ast::TraitFunction {
            name: name.into(),
            parameters: params,
            can_error: err_flag.is_some(),
            is_member: false,
        };
        
        if let Some("self") = func.parameters.first().map(|s| &s[..]) {
            func.parameters.remove(0);
            func.is_member = true;
        }

        func
    },
};

Block: ast::Block = {
    "{" <Statement*> "}" => ast::Block {
        statements: <>,
    }
};

Statement: ast::Statement = {
    "let" <Identifier> <("," <Identifier>)*> <("=" <Expression>)?> ";" => ast::Statement::Declaration(<>),
    "return" <Expression> <("," <Expression>)*> ";" => ast::Statement::Return(<>),
    "throw" <Expression> ";" => ast::Statement::Throw(<>),
    "use" <Expression> ";" => ast::Statement::Use(<>),
    "if" <IfBlockChain> => ast::Statement::IfElse(<>),
    "break" <Label?> ";" => ast::Statement::Break(<>),
    "continue" <Label?> ";" => ast::Statement::Continue(<>),

    <(<Label> ":")?> "loop" <Block> => {
        let (label, block) = (<>);
        let iloop = ast::Loop {
            label: label,
            block: block,
        };
        ast::Statement::Loop(iloop)
    },

    <(<Label> ":")?> "for" <Identifier> <("," <Identifier>)*> "in" <Expression> <Block> => {
        let (label, id, mut bindings, iter, block) = (<>);
        bindings.insert(0, id);
        let for_loop = ast::ForLoop {
            label: label,
            bindings: bindings,
            iterator: iter,
            block: block,
        };
        ast::Statement::ForLoop(for_loop)
    },

    <(<Label> ":")?> "while" <Expression> <Block> => {
        let (label, condition, block) = (<>);
        let while_loop = ast::WhileLoop {
            label: label,
            condition: condition,
            block: block,
        };
        ast::Statement::WhileLoop(while_loop)
    },

    <MemberAccess> <("," <MemberAccess>)*> <AssignOps> <Expression> ";" => ast::Statement::Assignment(<>),
    <FunctionCall> ";" => ast::Statement::Expression(<>),
};

AssignOps: ast::AssignOp = {
    "=" => ast::AssignOp::Assign,
    "||=" => ast::AssignOp::LogicalOr,
    "&&=" => ast::AssignOp::LogicalAnd,
    "|=" => ast::AssignOp::BitOr,
    "&=" => ast::AssignOp::BitAnd,
    "^=" => ast::AssignOp::BitXor,
    "<<=" => ast::AssignOp::LShiftLeft,
    ">>=" => ast::AssignOp::AShiftRight,
    ">>>=" => ast::AssignOp::LShiftRight,
    "+=" => ast::AssignOp::Add,
    "-=" => ast::AssignOp::Sub,
    "*=" => ast::AssignOp::Mul,
    "/=" => ast::AssignOp::Div,
    "%=" => ast::AssignOp::Rem,
    "mod=" => ast::AssignOp::Mod,
};

IfBlockChain: ast::IfElse = {
    <cond:Expression> <if_block:Block> => ast::IfElse {
        condition: Box::new(cond),
        if_block: Box::new(if_block),
        else_block: None,
    },
    <cond:Expression> <if_block:Block> "else" <else_block:Block> => ast::IfElse {
        condition: Box::new(cond),
        if_block: Box::new(if_block),
        else_block: Some(Box::new(else_block)),
    },
    <cond:Expression> <if_block:Block> "else" "if" <chain:IfBlockChain> => ast::IfElse {
        condition: Box::new(cond),
        if_block: Box::new(if_block),
        else_block: Some(Box::new(ast::Block {
            statements: vec![ast::Statement::IfElse(chain)],
        })),
    }
};

Expression: ast::Expression = {
    "new_object" <Identifier> <ObjectLiteral> => ast::Expression::ObjectConstructor(<>),
    <MemberAccess> "?" => ast::Expression::Try(Box::new(<>)),

    "fn" "(" <Params> ")" <"?"?> <Block> => ast::lambda(<>),
    
    "|" <Params> "|" <"?"?> <Expression> => ast::expr_lambda(<>),
    "||" <"?"?> <Expression> => ast::expr_lambda(vec![], <>),

    BinaryOperators,
};

ImplOp: ast::BinOp = {
    "impls" => ast::BinOp::Implements,
};

RangeOps: ast::BinOp = {
    ".." => ast::BinOp::RangeExclusive,
    "..." => ast::BinOp::RangeInclusive,
};

LogicalOr: ast::BinOp = {
    "||" => ast::BinOp::LogicalOr
};

LogicalAnd: ast::BinOp = {
    "&&" => ast::BinOp::LogicalAnd
};

EqualOps: ast::BinOp = {
    "==" => ast::BinOp::Equal,
    "!=" => ast::BinOp::NotEqual,
    "<" => ast::BinOp::LessThan,
    ">" => ast::BinOp::GreaterThan,
    "<=" => ast::BinOp::LessOrEqual,
    ">=" => ast::BinOp::GreaterOrEqual,
};

BitOps: ast::BinOp = {
    "&" => ast::BinOp::BitAnd,
    "|" => ast::BinOp::BitOr,
    "^" => ast::BinOp::BitXor,
};

ShiftOps: ast::BinOp = {
    "<<" => ast::BinOp::LShiftLeft,
    ">>" => ast::BinOp::AShiftRight,
    ">>>" => ast::BinOp::LShiftRight,
};

AddSubOps: ast::BinOp = {
    "+" => ast::BinOp::Add,
    "-" => ast::BinOp::Sub,
};

MulDivOps: ast::BinOp = {
    "*" => ast::BinOp::Mul,
    "/" => ast::BinOp::Div,
    "%" => ast::BinOp::Rem,
    "/%" => ast::BinOp::DivRem,
    "mod" => ast::BinOp::Mod,
};

BinaryOperators = ImplOperator;
ImplOperator = Tier<ImplOp, RangeOperators>;
RangeOperators = Tier<RangeOps, LogicalOperators>;
LogicalOperators = Tier<LogicalOr, Tier<LogicalAnd, EqualityOperators>>;
EqualityOperators = Tier<EqualOps, BitwiseOperators>;
BitwiseOperators = Tier<BitOps, ShiftOperators>;
ShiftOperators = Tier<ShiftOps, AddSubOperators>;
AddSubOperators = Tier<AddSubOps, MulDivOperators>;
MulDivOperators = Tier<MulDivOps, UnaryOperators>;

UnaryOperators: ast::Expression = {
    "-" <UnaryOperators> => ast::Expression::Negate(Box::new(<>)),
    "!" <UnaryOperators> => ast::Expression::Not(Box::new(<>)),
    //"-" <FunctionCall> => ast::Expression::Negate(Box::new(<>)),
    //"!" <FunctionCall> => ast::Expression::Not(Box::new(<>)),
    MemberAccess,
};

MemberAccess: ast::Expression = {
    <obj:MemberAccess> "[" <index:Expression> "]" => {
        ast::Expression::IndexAccess(Box::new(obj), Box::new(index))
    },
    <obj:MemberAccess> "." <member:Identifier> => {
        ast::Expression::MemberAccess(Box::new(obj), member)
    },
    FunctionCall,
    Term,
};

FunctionCall: ast::Expression = {
    <func:MemberAccess> "(" <params:Comma<Expression>> ")" => {
        ast::Expression::FunctionCall(Box::new(func), params)
    },
};

Term: ast::Expression = {
    <Literal> => ast::Expression::Literal(<>),
    <Identifier> => ast::Expression::Identifier(<>),
    "(" <Expression> ")",
    "nil" => ast::Expression::Nil,
};

Literal: ast::Literal = {
    <IntegerLiteral> => ast::Literal::Integer(<>),
    <FLoatLiteral> => ast::Literal::Float(<>),
    <StringLiteral> => ast::Literal::String(<>),
    <ObjectLiteral> => ast::Literal::Object(<>),
    <ArrayLiteral> => ast::Literal::Array(<>),

    "<" <Comma<UnaryOperators>> <(":" <Identifier>)?> ">" => ast::Literal::Simd(<>),
    "<" <UnaryOperators> "..." <(":" <Identifier>)?> ">" => {
        let (expr, id) = (<>);
        ast::Literal::SimdSplat(Box::new(expr), id)
    },
};

IntegerLiteral: i64 = {
    INT_LITERAL => i64::from_str_radix(<>, 10).unwrap(),
    HEX_LITERAL => i64::from_str_radix(<>, 16).unwrap(),
    OCT_LITERAL => i64::from_str_radix(<>, 8).unwrap(),
    BIN_LITERAL => i64::from_str_radix(<>, 2).unwrap(),
};

FLoatLiteral: f64 = {
    FLOAT_LITERAL => f64::from_str(<>).unwrap(),
};

// TODO: Parse string literal into actual string data
StringLiteral: String = {
    STR_LITERAL => (<>).into()
};

ObjectLiteral: ast::ObjectLiteral = {
    "{" <Comma<BlockItem>> "}" => (<>).into_iter().collect()
};

ArrayLiteral = "[" <Comma<Expression>> "]";

BlockItem: (ast::Identifier, ast::Expression) = {
    <Identifier> ":" <Expression> => (<>),
    <Identifier> => (<>.clone(), ast::Expression::Identifier(<>.clone())),
};

Identifier: String = IDENTIFIER => <>.into();
Label: String = LABEL => (<>[1..]).into();

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Tier<Op, NextTier>: ast::Expression = {
    <l:Tier<Op, NextTier>> <o:Op> <r:NextTier> => {
        ast::Expression::BinaryOp(Box::new(l), o, Box::new(r))
    },
    NextTier
};

extern {
    type Location = lexer::Location;
    type Error = lexer::LexicalError;

    enum lexer::Tok<'input> {
        "break" => (lexer::TokenType::Break, <&'input str>),
        "continue" => (lexer::TokenType::Continue, <&'input str>),
        "else" => (lexer::TokenType::Else, <&'input str>),
        "enum" => (lexer::TokenType::Enum, <&'input str>),
        "extern" => (lexer::TokenType::Extern, <&'input str>),
        "fn" => (lexer::TokenType::Function, <&'input str>),
        "for" => (lexer::TokenType::For, <&'input str>),
        "if" => (lexer::TokenType::If, <&'input str>),
        "impl" => (lexer::TokenType::Impl, <&'input str>),
        "impls" => (lexer::TokenType::Impls, <&'input str>),
        "in" => (lexer::TokenType::In, <&'input str>),
        "let" => (lexer::TokenType::Let, <&'input str>),
        "loop" => (lexer::TokenType::Loop, <&'input str>),
        "mod" => (lexer::TokenType::Mod, <&'input str>),
        "new_object" => (lexer::TokenType::NewObject, <&'input str>),
        "nil" => (lexer::TokenType::Nil, <&'input str>),
        "return" => (lexer::TokenType::Return, <&'input str>),
        "throw" => (lexer::TokenType::Throw, <&'input str>),
        "trait" => (lexer::TokenType::Trait, <&'input str>),
        "type" => (lexer::TokenType::Type, <&'input str>),
        "use" => (lexer::TokenType::Use, <&'input str>),
        "while" => (lexer::TokenType::While, <&'input str>),

        "{" => (lexer::TokenType::OpenCurly, <&'input str>),
        "}" => (lexer::TokenType::CloseCurly, <&'input str>),
        "(" => (lexer::TokenType::OpenParen, <&'input str>),
        ")" => (lexer::TokenType::CloseParen, <&'input str>),
        "[" => (lexer::TokenType::OpenBracket, <&'input str>),
        "]" => (lexer::TokenType::CloseBracket, <&'input str>),
        ";" => (lexer::TokenType::Semicolon, <&'input str>),
        ":" => (lexer::TokenType::Colon, <&'input str>),
        "," => (lexer::TokenType::Comma, <&'input str>),
        "?" => (lexer::TokenType::Question, <&'input str>),
        "." => (lexer::TokenType::Dot, <&'input str>),

        ".." => (lexer::TokenType::RangeExclusive, <&'input str>),
        "..." => (lexer::TokenType::RangeInclusive, <&'input str>),

        "==" => (lexer::TokenType::Equal, <&'input str>),
        "!=" => (lexer::TokenType::NotEqual, <&'input str>),
        "<" => (lexer::TokenType::LessThan, <&'input str>),
        "<=" => (lexer::TokenType::LessOrEqual, <&'input str>),
        ">" => (lexer::TokenType::GreaterThan, <&'input str>),
        ">=" => (lexer::TokenType::GreaterOrEqual, <&'input str>),

        "!" => (lexer::TokenType::Not, <&'input str>),
        "~" => (lexer::TokenType::Tilde, <&'input str>),
        "@" => (lexer::TokenType::At, <&'input str>),

        "&&" => (lexer::TokenType::LogicalAnd, <&'input str>),
        "||" => (lexer::TokenType::LogicalOr, <&'input str>),
        "&&=" => (lexer::TokenType::LogicalAndAssign, <&'input str>),
        "||=" => (lexer::TokenType::LogicalOrAssign, <&'input str>),

        "+" => (lexer::TokenType::Add, <&'input str>),
        "-" => (lexer::TokenType::Sub, <&'input str>),
        "*" => (lexer::TokenType::Mul, <&'input str>),
        "/" => (lexer::TokenType::Div, <&'input str>),
        "%" => (lexer::TokenType::Rem, <&'input str>),
        "/%" => (lexer::TokenType::DivRem, <&'input str>),
        "&" => (lexer::TokenType::BitAnd, <&'input str>),
        "|" => (lexer::TokenType::BitOr, <&'input str>),
        "^" => (lexer::TokenType::BitXor, <&'input str>),
        "<<" => (lexer::TokenType::Shl, <&'input str>),
        ">>" => (lexer::TokenType::Shr, <&'input str>),
        ">>>" => (lexer::TokenType::LShr, <&'input str>),

        "=" => (lexer::TokenType::Assign, <&'input str>),
        "+=" => (lexer::TokenType::AddAssign, <&'input str>),
        "-=" => (lexer::TokenType::SubAssign, <&'input str>),
        "*=" => (lexer::TokenType::MulAssign, <&'input str>),
        "/=" => (lexer::TokenType::DivAssign, <&'input str>),
        "%=" => (lexer::TokenType::RemAssign, <&'input str>),
        "&=" => (lexer::TokenType::BitAndAssign, <&'input str>),
        "|=" => (lexer::TokenType::BitOrAssign, <&'input str>),
        "^=" => (lexer::TokenType::BitXorAssign, <&'input str>),
        "<<=" => (lexer::TokenType::ShlAssign, <&'input str>),
        ">>=" => (lexer::TokenType::ShrAssign, <&'input str>),
        ">>>=" => (lexer::TokenType::LShrAssign, <&'input str>),
        "mod=" => (lexer::TokenType::ModAssign, <&'input str>),

        DOC_COMMENT => (lexer::TokenType::DocComment, <&'input str>),
        MOD_DOC_COMMENT => (lexer::TokenType::ModuleDocComment, <&'input str>),
        IDENTIFIER => (lexer::TokenType::Identifier, <&'input str>),
        INT_LITERAL => (lexer::TokenType::IntLiteral, <&'input str>),
        HEX_LITERAL => (lexer::TokenType::HexLiteral, <&'input str>),
        OCT_LITERAL => (lexer::TokenType::OctLiteral, <&'input str>),
        BIN_LITERAL => (lexer::TokenType::BinLiteral, <&'input str>),
        FLOAT_LITERAL => (lexer::TokenType::FloatLiteral, <&'input str>),
        STR_LITERAL => (lexer::TokenType::StringLiteral, <&'input str>),
        CHAR_LITERAL => (lexer::TokenType::CharLiteral, <&'input str>),
        LABEL => (lexer::TokenType::Label, <&'input str>),
    }
}
